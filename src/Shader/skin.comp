#include "common.glsli"

LayoutGroupSize(Skin);

layout(push_constant) 
uniform PushConstants
{
    u64 SkinAddress;
    u64 SrcAddress;
    u64 DstAddress;
    uint Count;
};

void main()
{
    skin_r Skin = skin_r(SkinAddress);
    vertex_buffer_r SrcVB = vertex_buffer_r(SrcAddress);
    vertex_buffer_w DstVB = vertex_buffer_w(DstAddress);

    uint Index = gl_GlobalInvocationID.x;

    if (Index < Count)
    {
        v3 InP = SrcVB.Data[Index].P;
        v3 InN = SrcVB.Data[Index].N;
        v4 InT = SrcVB.Data[Index].T;
        v4 Weights = SrcVB.Data[Index].Weights;
        uvec4 JointIndices = uvec4(SrcVB.Data[Index].Joints);

        m4 J0 = Skin.Data[JointIndices[0]];
        m4 J1 = Skin.Data[JointIndices[1]];
        m4 J2 = Skin.Data[JointIndices[2]];
        m4 J3 = Skin.Data[JointIndices[3]];
        
        v3 P = 
            Weights[0] * TransformPoint(J0, InP) +
            Weights[1] * TransformPoint(J1, InP) + 
            Weights[2] * TransformPoint(J2, InP) +
            Weights[3] * TransformPoint(J3, InP);

        // TODO(boti): inverse transpose
        v3 N = 
            Weights[0] * TransformDirection(J0, InN) +
            Weights[1] * TransformDirection(J1, InN) +
            Weights[2] * TransformDirection(J2, InN) +
            Weights[3] * TransformDirection(J3, InN);
        N = normalize(N);

        v3 T = 
            Weights[0] * TransformDirection(J0, InT.xyz) +
            Weights[1] * TransformDirection(J1, InT.xyz) +
            Weights[2] * TransformDirection(J2, InT.xyz) +
            Weights[3] * TransformDirection(J3, InT.xyz);
        T = normalize(T);

        DstVB.Data[Index].P         = P;
        DstVB.Data[Index].N         = N;
        DstVB.Data[Index].T         = vec4(T, InT.w);
        DstVB.Data[Index].TexCoord  = SrcVB.Data[Index].TexCoord;
        DstVB.Data[Index].Weights   = SrcVB.Data[Index].Weights;
        DstVB.Data[Index].Joints    = SrcVB.Data[Index].Joints;
        DstVB.Data[Index].Color     = SrcVB.Data[Index].Color;
    }
}